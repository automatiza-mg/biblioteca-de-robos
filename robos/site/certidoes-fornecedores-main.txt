/# Para a variável %pasta%, é importante verificar se:
1. o nome da pasta contém caracteres especiais e se eles serão reconhecidos pelo robô ao salvar as certidões;
2. existe algum arquivo com o mesmo nome da pasta, isso pode gerar um 'autocompletar' em vez da ação de salvar a certidão no computador#/
DateTime.GetCurrentDateTime.Local DateTimeFormat: DateTime.DateTimeFormat.DateAndTime CurrentDateTime=> data_hora
Workstation.SetDefaultPrinter PrinterName: $'''Microsoft Print to PDF'''
Excel.LaunchExcel.LaunchAndOpenUnderExistingProcess Path: planilha Visible: True ReadOnly: False Instance=> fornecedores
# Definição da linha atual para que possa ser acrescida à variavel uma unidade para o fluxo rodar
Excel.GetFirstFreeRowOnColumn Instance: fornecedores Column: $'''M''' FirstFreeRowOnColumn=> linhaatual
Excel.GetFirstFreeColumnRow Instance: fornecedores FirstFreeRow=> qtde_a_conferir
LOOP WHILE (linhaatual) < (qtde_a_conferir)
    Excel.ReadFromExcel.ReadCell Instance: fornecedores StartColumn: $'''A''' StartRow: linhaatual ReadAsText: False CellValue=> cnpjatual
    Excel.ReadFromExcel.ReadCell Instance: fornecedores StartColumn: $'''B''' StartRow: linhaatual ReadAsText: True CellValue=> empresa
    Excel.ReadFromExcel.ReadCell Instance: fornecedores StartColumn: $'''C''' StartRow: linhaatual ReadAsText: True CellValue=> cep
    Excel.ReadFromExcel.ReadCell Instance: fornecedores StartColumn: $'''D''' StartRow: linhaatual ReadAsText: True CellValue=> município
    Excel.ReadFromExcel.ReadCell Instance: fornecedores StartColumn: $'''E''' StartRow: linhaatual ReadAsText: True CellValue=> estado
    CALL CRC_Cagef_MG
    Excel.ReadFromExcel.ReadCell Instance: fornecedores StartColumn: $'''F''' StartRow: linhaatual ReadAsText: True CellValue=> cagef
    IF cagef <> $'''ativo''' THEN
        CALL CNPJ
        CALL CND_Federal
        CALL FGTS
        CALL Justiça_Trabalho
        IF estado = $'''MG''' THEN
            CALL SIARE_SEF_MG
        ELSE
            Excel.WriteToExcel.WriteCell Instance: fornecedores Value: $'''Pesquisar CND no estado da sede da empresa''' Column: $'''I''' Row: linhaatual
            IF município = $'''BH''' THEN
                CALL CND_PBH
            ELSE
                Excel.WriteToExcel.WriteCell Instance: fornecedores Value: $'''Pesquisar CND no município da sede da empresa''' Column: $'''J''' Row: linhaatual
            END
        END
    END
    Excel.WriteToExcel.WriteCell Instance: fornecedores Value: data_hora Column: $'''M''' Row: linhaatual
    Variables.IncreaseVariable Value: linhaatual IncrementValue: 1
END
Excel.CloseExcel.CloseAndSave Instance: fornecedores
